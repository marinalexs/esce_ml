from snakemake.utils import validate

configfile: workflow.source_path("../config/config.yaml")
configfile: workflow.source_path("../config/style.yaml")

validate(config, workflow.source_path("../config/config.schema.yaml"))
validate(config, workflow.source_path("../config/style.schema.yaml"))

import itertools, glob


def expand_from_config(filename):
    """Expand a filename template using the config file."""
    return list(
        itertools.chain(
            *[
                # merge the experiment-specific config with the global config
                expand(filename, **{**config["experiments"][experiment],**config})
                # for each experiment
                for experiment in config["experiments"]
            ]
        )
    )

all_fits = expand_from_config(
    "results/{dataset}/fits/{models}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{sample_sizes}_{seeds}_{grid}.csv"
)
sample_complexity_results = expand_from_config(
    "results/{dataset}/statistics/{models}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.stats.json"
)
individual_plots = expand_from_config(
    "results/{dataset}/plots/individual/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{models}_{grid}.png"
)
hp_plots = expand_from_config(
    "results/{dataset}/plots/hps/{models}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.png",
)
by_feature_plots = expand_from_config(
    "results/{dataset}/plots/features/all-features_{features_cni}_{targets}_{targets_cni}_{matching}_{models}_{grid}.png"
)
by_target_plots = expand_from_config(
    "results/{dataset}/plots/targets/{features}_{features_cni}_all-targets_{targets_cni}_{matching}_{models}_{grid}.png"
)
by_model_plots = expand_from_config(
    "results/{dataset}/plots/models/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_all-models_{grid}.png",
)
by_cni_plots = expand_from_config(
    "results/{dataset}/plots/cni/{features}_all-features-cni_{targets}_all-targets-cni_all-matching_{models}_{grid}.png",
)


rule all:
    input:
        all_fits,
        sample_complexity_results,
        individual_plots,
        by_feature_plots,
        by_target_plots,
        by_model_plots,
        by_cni_plots,
        hp_plots,


rule prepare_features_or_targets:
    output:
        out="results/{dataset}/{features_or_targets}/{name}_none.h5",
    params:
        custom_datasets=config["custom_datasets"],
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/prepare_data.py")


rule prepare_covariates:
    output:
        out="results/{dataset}/covariates/{name}.h5",
    params:
        custom_datasets=config["custom_datasets"],
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/prepare_data.py")


rule confound_regression:
    input:
        features="results/{dataset}/{targets_or_features}/{name}_none.h5",
        confounds="results/{dataset}/covariates/{confounds}.h5",
    output:
        features="results/{dataset}/{targets_or_features}/{name}_{confounds}.h5",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/confound_regression.py")


rule split:
    input:
        features="results/{dataset}/features/{features}_{features_cni}.h5",
        targets="results/{dataset}/targets/{targets}_{targets_cni}.h5",
        matching="results/{dataset}/covariates/{matching}.h5",
    params:
        val_test_frac=config["val_test_frac"],
        val_test_max=config["val_test_max"],
        val_test_min=config["val_test_min"],
        stratify=config["stratify"],
    output:
        split="results/{dataset}/splits/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{samplesize}_{seed}.json",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/generate_splits.py")


rule fit:
    input:
        features="results/{dataset}/features/{features}_{features_cni}.h5",
        targets="results/{dataset}/targets/{targets}_{targets_cni}.h5",
        split="results/{dataset}/splits/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{samplesize}_{seed}.json",
        grid=lambda wildcards: workflow.source_path("../config/grids/{grid}.yaml".format(**wildcards)),
    params:
        existing_scores=lambda wildcards: glob.glob(
            "results/{dataset}/fits/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{samplesize}_{seed}_*.csv".format(
                **wildcards
            )
        ),
    output:
        scores="results/{dataset}/fits/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{samplesize}_{seed}_{grid}.csv",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/fit_model.py")


rule aggregate:
    input:
        scores=expand(
            "results/{{dataset}}/fits/{{model}}/{{features}}_{{features_cni}}_{{targets}}_{{targets_cni}}_{{matching}}_{samplesize}_{seed}_{{grid}}.csv",
            seed=config["seeds"],
            samplesize=config["sample_sizes"],
        ),
    output:
        scores="results/{dataset}/scores/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.csv",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/aggregate.py")


rule extrapolate:
    input:
        scores="results/{dataset}/scores/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.csv",
    params:
        bootstrap_repetitions=config["bootstrap_repetitions"],
    output:
        stats="results/{dataset}/statistics/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.stats.json",
        bootstraps="results/{dataset}/statistics/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.bootstrap.json",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/extrapolate.py")


rule plot_individually:
    input:
        sample_complexity_results,
    params:
        stats=[
            "results/{dataset}/statistics/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.stats.json",
        ],
        title = config['figure_titles']['individual'],
        color_variable=None,
        linestyle_variable=None,
        max_x=config["extrapolate_to"],
    output:
        plot="results/{dataset}/plots/individual/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{model}_{grid}.png",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/plot.py")


rule plot_by_features:
    input:
        sample_complexity_results,
    params:
        stats=lambda wildcards: glob.glob(
            "results/{dataset}/statistics/{model}/*_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.stats.json".format(
                **wildcards
            )
        ),
        title=config['figure_titles']['features'],
        color_variable="features",
        linestyle_variable=None,
        max_x=config["extrapolate_to"],
    output:
        plot="results/{dataset}/plots/features/all-features_{features_cni}_{targets}_{targets_cni}_{matching}_{model}_{grid}.png",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/plot.py")


rule plot_by_targets:
    input:
        sample_complexity_results,
    params:
        stats=lambda wildcards: glob.glob(
            "results/{dataset}/statistics/{model}/{features}_{features_cni}_*_{targets_cni}_{matching}_{grid}.stats.json".format(
                **wildcards
            )
        ),
        title= config['figure_titles']['targets'],
        color_variable="target",
        linestyle_variable=None,
        max_x=config["extrapolate_to"],
    output:
        plot="results/{dataset}/plots/targets/{features}_{features_cni}_all-targets_{targets_cni}_{matching}_{model}_{grid}.png",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/plot.py")


rule plot_by_models:
    input:
        sample_complexity_results,
    params:
        stats=lambda wildcards: glob.glob(
            "results/{dataset}/statistics/*/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.stats.json".format(
                **wildcards
            )
        ),
        title = config['figure_titles']['models'],
        color_variable="model",
        linestyle_variable=None,
        max_x=config["extrapolate_to"],
    output:
        plot="results/{dataset}/plots/models/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_all-models_{grid}.png",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/plot.py")


rule plot_by_cni:
    input:
        sample_complexity_results,
    params:
        stats=lambda wildcards: glob.glob(
            "results/{dataset}/statistics/{model}/{features}_*_{targets}_*_*_{grid}.stats.json".format(
                **wildcards
            )
        ),
        title=config['figure_titles']['cni'],
        color_variable="cni",
        linestyle_variable=None,
        max_x=config["extrapolate_to"],
    output:
        plot="results/{dataset}/plots/cni/{features}_all-features-cni_{targets}_all-targets-cni_all-matching_{model}_{grid}.png",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/plot.py")


rule plot_hyperparameters:
    input:
        scores="results/{dataset}/scores/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.csv",
    params:
        title=config['figure_titles']['hyperparameters'],
        hyperparameter_scales=config["hyperparameter_scales"],
        grid=lambda wildcards: workflow.source_path("../config/grids/{grid}.yaml".format(**wildcards)),
    output:
        plot="results/{dataset}/plots/hps/{model}/{features}_{features_cni}_{targets}_{targets_cni}_{matching}_{grid}.png",
    conda:
        workflow.source_path("envs/environment.yaml")
    script:
        workflow.source_path("scripts/plot_hps.py")


rule compress_results:
    input:
        rules.all.input,
    output:
        "results_{dataset}.tar.gz",
    shell:
        """
        tar -cvpzf results_{wildcards.dataset}.tar.gz results/{wildcards.dataset}/statistics results/{wildcards.dataset}/scores
        """
